if (!project.hasProperty('cCompilerOptimisationLevel')) {
    println "==***=== setting default c optimisation level: -Os"
    project.ext.cCompilerOptimisationLevel = '-Os'
} else {
    println "==***=== c optimisation level overridden in config: " + project.ext.cCompilerOptimisationLevel
}

binaries.all {
    cCompiler.args "-mmcu=${djMcu}"
    cCompiler.args project.ext.cCompilerOptimisationLevel
    cCompiler.args '-finline-limit-0'
    cCompiler.args '-ggdb'
    cCompiler.args '-Wall'
    cCompiler.args '-Werror'
    cCompiler.args '-std=gnu99'
    cCompiler.args '-fdata-sections'
    cCompiler.args '-ffunction-sections'
    linker.args "-mmcu=${djMcu}"
    linker.args "-Wl,--section-start=.reprogram_flash_page=${djReprogramFlashPageAddress}"
    linker.args '-Wl,--gc-sections'
}

if (!project.hasProperty('no-rtc-guards')) {
    binaries.all {
        cCompiler.args '-DRTC_GUARDS=1'
    }
}

// AVR tasks: transform infusion archives to c arrays
// and add those C files to the application model
task createCArrays {
    dependsOn tasks['createLibArchive']
    dependsOn tasks['createAppArchive']
    ext.destDir = new File("${buildDir}/carrays")
    def appArchiveFile = tasks['createAppArchive'].archiveFile
    def appArchiveCFile = "${destDir}/app_infusiondja.c"
    def libArchiveFile = tasks['createLibArchive'].archiveFile
    def libArchiveCFile = "${destDir}/lib_infusionsdja.c"

    inputs.file appArchiveFile
    inputs.file libArchiveFile
    outputs.dir destDir

    doLast {
        ensureEmptyDir(destDir)
        ant.carray(dest: libArchiveCFile,
                             src: libArchiveFile,
                             arrayname: 'di_lib_infusions_archive',
                             arraysize: 0,
                             keywords: 'AVR')
        ant.carray(dest: appArchiveCFile,
                             src: appArchiveFile,
                             arrayname: 'di_app_infusion_archive',
                             arraysize: djConfig.appArchiveSize,
                             keywords: 'AVR PAGEALIGN')
    }
}

model {
    components.darjeeling {
        targetPlatform 'avr'
        sources {
            main(CSourceSet) {  
                source {
                    srcDir createCArrays.destDir
                    include '**/*.c'
                    builtBy(createCArrays)
                }
            }
        }
    }
    toolChains {
        gcc(Gcc) {
            target('avr') { t ->
                t.getcCompiler().setExecutable('avr-gcc');
                t.getCppCompiler().setExecutable('avr-g++');
                t.getObjcCompiler().setExecutable('avr-gcc');
                t.getObjcppCompiler().setExecutable('avr-g++');
                t.getAssembler().setExecutable('avr-as');
                t.getLinker().setExecutable('avr-gcc');
                t.getStaticLibArchiver().setExecutable('avr-ar');
            }
        }
    }
    platforms {
        avr {
            architecture 'avr'
        }
    }
}

tasks.architectureSpecificAssemble {
    ext.djTargetElfFile = new File("${buildDir}/darjeeling/darjeeling.elf")
    ext.djIHex = "${destDir}/darjeeling.ihex"
    ext.djEepromIHex = "${destDir}/darjeeling_eeprom.ihex"

    inputs.file djTargetBinary

    doLast {
        // Create darjeeling.ihex
        exec {
            executable 'avr-objcopy'
            args '-j', '.text', '-j', '.data', '-O', 'ihex', djTargetBinary, djIHex
        }
        // Create darjeeling_eeprom.ihex
        exec {
            executable 'avr-objcopy'
            args '-j', '.eeprom', '-O', 'ihex', djTargetBinary, djEepromIHex
        }
        copy {
            from djTargetBinary into destDir rename { f -> f+'.elf' }
        }
    }
}


tasks.darjeeling {
    doLast {
        exec {
            executable 'avr-objdump'
            args '-Pmem-usage', architectureSpecificAssemble.djTargetElfFile
        }
    }
}

/// AVR helper tasks
task avrdude(dependsOn: darjeeling) << {
    exec {
        executable 'avrdude'
        args '-p', djMcu, '-P', avrdudeprogrammer, '-c', 'wiring', '-U', "flash:w:${architectureSpecificAssemble.djIHex}"
    }
}

task avrdude_eeprom(dependsOn: darjeeling) << {
    exec {
        executable 'avrdude'
        args '-p', djMcu, '-P', avrdudeprogrammer, '-c', 'wiring', '-U', "flash:w:${architectureSpecificAssemble.djIHex}", '-U', "eeprom:w:${architectureSpecificAssemble.djEepromIHex}"
    }
}

task avarice << {
    exec {
        executable 'avarice'
        args '-g', '-j', 'usb', '-B', '4000000', ':4242'
    }
}

task vm_sizenm (type: Exec, dependsOn: darjeeling) {
    ext.nmOutput = "${buildDir}/nmdata.txt"

    inputs.file architectureSpecificAssemble.djTargetElfFile
    outputs.file nmOutput

    executable 'avr-nm'
    args '-S', '--size-sort', architectureSpecificAssemble.djTargetElfFile
    standardOutput = new ByteArrayOutputStream()
    doLast {
        new File(nmOutput).write(standardOutput.toString())
    }
}

task vm_size (type: Exec, dependsOn: vm_sizenm) {
    def fsxScript = "${project.rootDir}/config/${djConfigname}/Vmsize.fsx"

    executable 'fsharpi'
    args '--exec', fsxScript, vm_sizenm.nmOutput
}

task avrora(dependsOn: darjeeling) {
    ext.rtcdataFile = "${buildDir}/rtcdata.xml"
    ext.profiledataFile = "${buildDir}/profilerdata.xml"
    def avroraJar = '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar'

    doLast {
        javaexec {
            main '-jar'
            args avroraJar, '-monitors=memory,c-timer,c-print,rtc,profile,stack', "-rtc-data-filename=${rtcdataFile}", "-rtc-gradle-build=${buildDir}", "-profile-data-filename=${profiledataFile}", '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
        }
    }
}

task avrora_if_necessary(dependsOn: darjeeling) {
    ext.rtcdataFile = "${buildDir}/rtcdata.xml"
    ext.profiledataFile = "${buildDir}/profilerdata.xml"
    ext.stdoutlogFile = "${buildDir}/stdoutlog.txt"

    def avroraJar = '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar'

    outputs.file rtcdataFile
    outputs.file profiledataFile
    outputs.file stdoutlogFile
    inputs.file architectureSpecificAssemble.djTargetElfFile
    inputs.file avroraJar

    project.ext.stdoutLog = ""
    logging.addStandardOutputListener(
        new StandardOutputListener() {
            @Override
            void onOutput(CharSequence charSequence) {
                project.ext.stdoutLog += charSequence.toString()
            }
        }
    )

    doLast {
        javaexec {
            main '-jar'
            args avroraJar, '-monitors=memory,c-timer,c-print,rtc,profile,stack', "-rtc-data-filename=${rtcdataFile}", "-rtc-gradle-build=${buildDir}", "-profile-data-filename=${profiledataFile}", '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
        }
        new File(stdoutlogFile).write(project.ext.stdoutLog)
    }
}

task disassemble(type:Exec, dependsOn: darjeeling) {
    ext.disasmFile = "${buildDir}/darjeeling.S"

    inputs.file architectureSpecificAssemble.djTargetElfFile
    outputs.file disasmFile

    executable "avr-objdump"
    args '-D', architectureSpecificAssemble.djTargetElfFile

    doFirst {
        standardOutput = new FileOutputStream(disasmFile)
    }
}

task avr_nm(type:Exec, dependsOn: darjeeling) {
    ext.nmFile = "${buildDir}/darjeeling.nm"

    inputs.file architectureSpecificAssemble.djTargetElfFile
    outputs.file nmFile

    executable "avr-nm"
    args '-S', '-l', architectureSpecificAssemble.djTargetElfFile

    doFirst {
        standardOutput = new FileOutputStream(nmFile)
    }
}

task avrora_store_trace(type:Copy, dependsOn: [avrora_if_necessary, disassemble, avr_nm]) {
    from file("${buildDir}/infusion-bm_${project.ext.aotbm}/bm_${project.ext.aotbm}.dih")
    from file("${buildDir}/infusion-bm_${project.ext.aotbm}/jlib_bm_${project.ext.aotbm}.debug")
    from file(avrora_if_necessary.rtcdataFile)
    from file(avrora_if_necessary.profiledataFile)
    from file(avrora_if_necessary.stdoutlogFile)
    from file(disassemble.disasmFile)
    from file(avr_nm.nmFile)
    if (djConfigname == "coremk_c")
        into file("${project.rootDir}/config/${djConfigname}/results_coremk_c")
    else
        into file("${project.rootDir}/config/${djConfigname}/${project.ext.aotTraceResultsDirName}/${project.ext.aotbm}")
}

task avrora_analyse_trace(type:Exec, dependsOn: [avrora_store_trace]) {
    def fsxScript = "${project.rootDir}/config/${djConfigname}/process-output/process-output/bin/Debug/process-output.exe"
    def tracesOutputDir = new File("${project.rootDir}/config/${djConfigname}/${project.ext.aotTraceResultsDirName}/${project.ext.aotbm}")

    executable "mono"
    args fsxScript, "process", tracesOutputDir
}

task avrora_rtcdata_toscreen(dependsOn: darjeeling) << {
    javaexec {
        main '-jar'
        args '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar', '-monitors=memory,c-timer,rtc,profile,c-print', "-profile-data-filename=${buildDir}/profilerdata", '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
    }
}

task avrora_notrace(dependsOn: darjeeling) << {
    javaexec {
        main '-jar'
        args '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar', '-monitors=memory,c-timer,c-print', '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
    }
}

task avrora_tracertc(dependsOn: darjeeling) << {
    javaexec {
        main '-jar'
        args '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar', '-monitors=memory,c-timer,rtc,c-print,trace', '-trace-only-when-enabled=true', '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
    }
}

task avrora_gdb(dependsOn: darjeeling) << {
    javaexec {
        main '-jar'
        args '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar', '-monitors=memory,c-timer,rtc,gdb,c-print,trace', '-trace-only-when-enabled=true', '-port=4242', '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
    }
}

task avrora_jdb(dependsOn: darjeeling) << {
    javaexec {
        main '-jar'
        args '-agentlib:jdwp=transport=dt_socket,address=8000,server=y,suspend=n', '/Users/nielsreijers/src/avrora/jars/avrora-beta-1.7.117.jar', '-monitors=memory,c-timer,rtc,c-print', '-trace-only-when-enabled=true', '-port=4242', '-single', "-mcu=${djMcu}", '-dump-writes', '-locations=0x00,0x01,0x02,0x03,0x04,0x05,0x06,0x07,0x08,0x09,0x0a,0x0b,0x0c,0x0d,0x0e,0x0f,0x10,0x11,0x12,0x13,0x14,0x15,0x16,0x17,0x18,0x19,0x1a,0x1b,0x1c,0x1d,0x1e,0x1f', architectureSpecificAssemble.djTargetElfFile
    }
}
